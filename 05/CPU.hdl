// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Compute whether to write Alu output to destination registers
    And(a=instruction[3], b=instruction[15], out=writeM);
    And(a=instruction[4], b=instruction[15], out=writeD);
    And(a=instruction[5], b=instruction[15], out=writeCompA);
    Not(in=instruction[15], out=aInstruction);

     // Compute which value to supply as input to A register
    Or(a=aInstruction, b=writeCompA, out=writeA);
    Mux16(a=instruction, b=outAlu, sel=instruction[15], out=AValue);
    
    // A and D registers
    ARegister(in=AValue, load=writeA, out=A, out[0..14]=addressM);
    DRegister(in=outAlu, load=writeD, out=D);

    // Decide whether A or M to be computed
    Mux16(a=A, b=inM, sel=instruction[12], out=compY);
    // Alu computation    
    ALU(x=D, y=compY, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outAlu, out=outM, zr=zr, ng=ng);

    // Compute outAlu > 0
    Or(a=zr, b=ng, out=notGt);
    Not(in=notGt, out=gt);


    // Line up JMP bits with comp result
    And16(a[0..2]=instruction[0..2], a[3..15]=false, b[0]=gt, b[1]=zr, b[2]=ng, b[3..15]=false, out[0..7]=jmpArr);
    // Jump if we have a match
    Or8Way(in=jmpArr, out=jmp);

    And(a=jmp, b=instruction[15], out=loadPC); // Only load if not A instruction
    PC(in=A, load=loadPC, inc=true, reset=reset, out[0..14]=pc);
}    